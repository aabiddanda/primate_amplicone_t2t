#!python3

import numpy as np
import pandas as pd


configfile: "config.yaml"


rule all:
    input:
        expand(
            "results/{species}/ref.chrY.{feature}.bed",
            feature=["mappability", "trf"],
            species=list(config["species"].keys()),
        ),
        expand(
            "results/{species}/ref.amplicone.chrY.gene_def.tab",
            species=list(config["species"].keys()),
        ),


rule download_fasta_fastaidx:
    """Download fasta file from remote repositories. """
    output:
        fasta="results/{species}/ref.fasta.gz",
        faidx="results/{species}/ref.fasta.gzi",
    params:
        fasta=lambda wildcards: config["species"][wildcards.species]["fasta"],
        faidx=lambda wildcards: config["species"][wildcards.species]["faidx"],
    resources:
        time="0:30:00",
        mem_mb="5G",
    shell:
        """
        wget {params.fasta} -O {output.fasta}
        wget {params.faidx} -O {output.faidx} 
        """


rule ref_specific_mappability:
    """Generate reference-specific mappability metrics for AmpliCONE."""
    input:
        ref_fasta="results/{species}/ref.fasta.gz",
    output:
        ref_fasta="results/{species}/ref.chrY.fa",
        gem_index="results/{species}/ref.chrY.fa.gem",
        mappability="results/{species}/ref.chrY.mappability",
        mappability_bg=temp("results/{species}/ref.chrY.mappability.bg"),
        mappability_bed="results/{species}/ref.chrY.mappability.bed",
    resources:
        time="3:00:00",
        mem_mb="10G",
    params:
        out_wig=lambda wildcards: f"results/{wildcards.species}/ref.chrY",
        out_wig2=lambda wildcards: f"results/{wildcards.species}/ref.chrY.mappability",
        read_size=lambda wildcards: config["species"][wildcards.species]["readsize"],
    threads: 24
    shell:
        """
       samtools faidx {input.ref_fasta} chrY > {output.ref_fasta}
       gem-indexer -i {output.ref_fasta} -o {output.ref_fasta} --complement emulate -T {threads} --verbose
       gem-mappability -I {output.gem_index} -l {params.read_size} -o {params.out_wig} -m 2 -e 2 -T {threads}
       gem-2-bed mappability -I {output.gem_index} -i {output.mappability} -o {params.out_wig2}
       cp {output.mappability_bg} {output.mappability_bed}
       """


rule ref_specific_repeatmasker:
    """Obtain reference-specific repeatmasker output."""
    input:
        ref_fasta=lambda wildcards: config["species"][wildcards.species]["fasta"],
        ref_masker=lambda wildcards: config["species"][wildcards.species][
            "repeatMasker"
        ],
    output:
        repeatmasker="results/{species}/ref.repeatmasker.out",
    shell:
        "cp {input.ref_masker} {output}"


rule split_fasta:
    input:
        fasta="results/{species}/ref.chrY.fa",
    output:
        temp(
            expand(
                "results/{{species}}/split_fasta/chunk{c}.fa.gz",
                c=[f"{x:03d}" for x in range(1, 61)],
            )
        ),
    shell:
        "kmcp utils split-genomes -m 1 -k 1 -n 60 -j 10 -l 0 {input.fasta} -O results/{wildcards.species}/split_fasta/ --force"


rule ref_specific_tandemrepeat:
    """Obtain reference-specific tandem repeat finder output.
    
    NOTE: this is just using the default 
    """
    input:
        fasta=lambda wildcards: f"results/{wildcards.species}/split_fasta/chunk{int(wildcards.c):03d}.fa.gz",
    output:
        tmp_fasta=temp("results/{species}/split_fasta/chunk{c}.fa"),
        tandem_repeat_dat=temp("results/{species}/trf/ref.chrY.chunk{c}.trf.dat"),
    shell:
        "zcat {input.fasta} > {output.tmp_fasta}; trf {output.tmp_fasta} 2 7 7 80 10 50 500 -h -ngs > {output.tandem_repeat_dat}"


rule collect_trf_dat:
    """Collect TRF Dat Output."""
    input:
        fastas=expand("results/{{species}}/split_fasta/chunk{c}.fa", c=range(1, 61)),
        trf_dats=expand(
            "results/{{species}}/trf/ref.chrY.chunk{c}.trf.dat", c=range(1, 61)
        ),
    output:
        meta_trf_dat="results/{species}/trf/ref.chrY.trf.dat",
    script:
        "scripts/combine_split_dat.py"


rule tandemrepeat2bed:
    """Convert tandem-repeat output to bed-output."""
    input:
        tandem_repeat_dat="results/{species}/trf/ref.chrY.trf.dat",
    output:
        tandem_repeat_bed="results/{species}/ref.chrY.trf.bed",
    wildcard_constraints:
        species="|".join(config["species"].keys()),
    script:
        "scripts/TRFdat_to_bed.py"


# ------- Obtain the canonical sequences for ampliconic genes from hg38 ------ #
rule isolate_chrY_seq:
    """Isolate the chrY sequences from hg38. """
    input:
        hg38_fasta=config["hg38_ref_data"]["fasta"],
        amplicone_hg38_gene_def=config["hg38_ref_data"]["gene_def"],
    output:
        regions_file=temp("results/grch38_ref/amplicon_hg38.regions.txt"),
        region_fasta="results/grch38_ref/amplicon_hg38.regions.fasta",
    shell:
        """
        awk \'NR > 1 {{print \"chrY:\"$1\"-\"$2}}\'  {input.amplicone_hg38_gene_def} > {output.regions_file} 
        samtools faidx -r {output.regions_file} {input.hg38_fasta} > {output.region_fasta} 
        """


rule obtain_canonical_sequences:
    """Obtain the canonical sequences for each of the underlying genes."""
    input:
        amplicone_hg38_gene_def=config["hg38_ref_data"]["gene_def"],
        region_fasta="results/grch38_ref/amplicon_hg38.regions.fasta",
    output:
        expand(
            "results/grch38_ref/amplicon.chrY.hg38.{gene}.fasta",
            gene=np.unique(
                pd.read_csv(config["hg38_ref_data"]["gene_def"], sep="\t")[
            "TYPE"
                ].values
            ),
        ),
    params:
        genes=lambda wildcards: np.unique(
            pd.read_csv(config["hg38_ref_data"]["gene_def"], sep="\t")["TYPE"].values
        ),
    script:
        "scripts/identify_canonical_seq.py"


rule create_gene_seq_match_seq:
    """Obtain the blast results for matching sequence."""
    input:
        fasta="results/{species}/ref.chrY.fa",
        seq_query="results/grch38_ref/amplicon.chrY.hg38.{gene}.fasta",
    output:
        gene_blast="results/{species}/blast/ref.chrY.{gene}.blast.txt",
    params:
        min_match=99,
    threads: 16
    shell:
        "pblat {input.fasta} {input.seq_query} {output.gene_blast} -t=dna -q=dna -threads={threads} -minIdentity={params.min_match} -out=blast9"


rule collect_regions:
    """Collect regions """
    input:
        expand(
            "results/{{species}}/blast/ref.chrY.{gene}.blast.txt",
            gene=np.unique(
                pd.read_csv(config["hg38_ref_data"]["gene_def"], sep="\t")[
            "TYPE"
                ].values
            ),
        ),
    output:
        "results/{species}/ref.amplicone.chrY.gene_def.tab",
    params:
        min_percent_match=99,
    shell:
        "cat {input} | grep -v \"#\" | awk ' $3 > {params.min_percent_match} {{print $9,$10,$1}}' > {output} "
