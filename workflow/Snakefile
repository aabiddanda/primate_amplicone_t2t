#!python3

import numpy as np
import pandas as pd


configfile: "config.yaml"


rule all:
    input:
        expand(
            "results/{species}/ref.chrY.{feature}.bed",
            feature=["mappability", "trf"],
            species=list(config.keys()),
        ),


rule download_fasta_fastaidx:
    """Download fasta file from remote repositories. """
    output:
        fasta="results/{species}/ref.fasta.gz",
        faidx="results/{species}/ref.fasta.gzi",
    params:
        fasta=lambda wildcards: config[wildcards.species]["fasta"],
        faidx=lambda wildcards: config[wildcards.species]["faidx"],
    resources:
        time="0:30:00",
        mem_mb="5G",
    shell:
        """
        wget {params.fasta} -O {output.fasta}
        wget {params.faidx} -O {output.faidx} 
        """


rule ref_specific_mappability:
    """Generate reference-specific mappability metrics for AmpliCONE."""
    input:
        ref_fasta="results/{species}/ref.fasta.gz",
    output:
        ref_fasta="results/{species}/ref.chrY.fa",
        gem_index="results/{species}/ref.chrY.fa.gem",
        mappability="results/{species}/ref.chrY.mappability",
        mappability_bg=temp("results/{species}/ref.chrY.mappability.bg"),
        mappability_bed="results/{species}/ref.chrY.mappability.bed",
    resources:
        time="3:00:00",
        mem_mb="10G",
    params:
        out_wig=lambda wildcards: f"results/{wildcards.species}/ref.chrY",
        out_wig2=lambda wildcards: f"results/{wildcards.species}/ref.chrY.mappability",
        read_size=lambda wildcards: config[wildcards.species]["readsize"],
    threads: 24
    shell:
        """
       samtools faidx {input.ref_fasta} chrY > {output.ref_fasta}
       gem-indexer -i {output.ref_fasta} -o {output.ref_fasta} --complement emulate -T {threads} --verbose
       gem-mappability -I {output.gem_index} -l {params.read_size} -o {params.out_wig} -m 2 -e 2 -T {threads}
       gem-2-bed mappability -I {output.gem_index} -i {output.mappability} -o {params.out_wig2}
       cp {output.mappability_bg} {output.mappability_bed}
       """


rule ref_specific_repeatmasker:
    """Obtain reference-specific repeatmasker output."""
    input:
        ref_fasta=lambda wildcards: config[wildcards.species]["fasta"],
        ref_masker=lambda wildcards: config[wildcards.species]["repeatMasker"],
    output:
        repeatmasker="results/{species}/ref.repeatmasker.out",
    shell:
        "cp {input.ref_masker} {output}"


rule split_fasta:
    input:
        fasta="results/{species}/ref.chrY.fa",
    output:
        temp(
            expand(
                "results/{{species}}/split_fasta/chunk{c}.fa.gz",
                c=[f"{x:03d}" for x in range(1, 61)],
            )
        ),
    shell:
        "kmcp utils split-genomes -m 1 -k 1 -n 60 -j 10 -l 0 {input.fasta} -O results/{wildcards.species}/split_fasta/ --force"


rule ref_specific_tandemrepeat:
    """Obtain reference-specific tandem repeat finder output.
    
    NOTE: this is just using the default 
    """
    input:
        fasta=lambda wildcards: f"results/{wildcards.species}/split_fasta/chunk{int(wildcards.c):03d}.fa.gz",
    output:
        tmp_fasta=temp("results/{species}/split_fasta/chunk{c}.fa"),
        tandem_repeat_dat=temp("results/{species}/trf/ref.chrY.chunk{c}.trf.dat"),
    shell:
        "zcat {input.fasta} > {output.tmp_fasta}; trf {output.tmp_fasta} 2 7 7 80 10 50 500 -h -ngs > {output.tandem_repeat_dat}"


rule collect_trf_dat:
    """Collect TRF Dat Output."""
    input:
        fastas=expand("results/{{species}}/split_fasta/chunk{c}.fa", c=range(1, 61)),
        trf_dats=expand(
            "results/{{species}}/trf/ref.chrY.chunk{c}.trf.dat", c=range(1, 61)
        ),
    output:
        meta_trf_dat="results/{species}/trf/ref.chrY.trf.dat",
    script:
        "scripts/combine_split_dat.py"


rule tandemrepeat2bed:
    """Convert tandem-repeat output to bed-output."""
    input:
        tandem_repeat_dat="results/{species}/trf/ref.chrY.trf.dat",
    output:
        tandem_repeat_bed="results/{species}/ref.chrY.trf.bed",
    script:
        "scripts/TRFdat_to_bed.py"


# rule create_gene_def_file:
# """Create gene definition file using pblat."""
# input:
# mappability_bed="results/{species}/ref.chrY.mappability.bed",
# repeat_masker="results/{species}/ref.repeatmasker.out",
# fasta="results/{species}/ref.chrY.fa",
# tandem_repeat_bed="results/{species}/ref.chrY.trf.bed",
# seq_query = config["y_amplicon_seqs"]
# output:
# gene_def="results/{species}/ref.chrY.gene_definition.tab",
# threads: 16
# shell:
#         "pblat {input.fasta} {input.seq_query} -t=dna -q=dna -threads={threads} {output.gene_def}"
